
#if ( "$TERM" != cons25 && "$TERM" != "at386" ) then 
#	set no_win_silent=true
#endif

#if ( ! $?LOGNAME && ! $?USER ) set no_win_silent=true
if ( $?LOGNAME ) then
	if ( $LOGNAME !~ sts ) set no_win_silent=true
endif
if ( $?USER ) then
	if ( $USER !~ sts ) set no_win_silent=true
endif

if ( -r $home/.nowindows ) set no_win=true

#if ( ! -o `tty` ) set no_win=true
if ( ! -o "$tty" ) set no_win=true

set xinit=()
if ( ! $?no_win ) then
	if ( $?HOST ) then
		grep $HOST $home/.csh_hosts.nowin >/dev/null
		if ( $status == 0 ) set no_win_silent=true
	endif

	foreach _xinit ( startx xinit )
		set _exe_xinit=`which $_xinit`

		switch ( "$_exe_xinit" )
		case *"not found"*:
			continue
			breaksw
		default:
			breaksw
		endsw

		if ( -x "$_exe_xinit" ) then
			unset no_win
			set path=($path `dirname $_exe_xinit`)
			set xinit=($_exe_xinit)
			break
		endif
	end
endif

if ( "$xinit" != "" ) then
	set what=(`basename $xinit`)
else
	set what=("window system")
	set no_win=(true)
endif

if ( $?no_win_silent ) then
	true
else if ( $?no_win ) then
	echo -n "csh [.login]: won't start "
	echo $what
else # if ( "$TERM" == cons25  || "$TERM" == "at386" ) then
	if ( ! -r $home/.nowinexec ) then
		echo -n "Starting window system... "
		exec $xinit
		# ought to be NOTREACHED
		# but if it ever fails, don't try again
	    	unsetenv RDISPLAY
	    	unsetenv DISPLAY
	    	echo "$xinit failed: "`date` > $home/.nowindows
	else
		echo "Starting window system... "
		echo "csh [.login]: no exec flag set, window system exit will not log out."
		$xinit
	endif
endif
